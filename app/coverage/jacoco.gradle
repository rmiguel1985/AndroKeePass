apply plugin: 'jacoco'

ext {
    coverageSourceDirs = 'src/test/java'
}

jacoco {
    toolVersion = "0.7.5.201505241946"
    reportsDir = file("$buildDir/reports")
}

task jacocoTestReport(type:JacocoReport, dependsOn: "testDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports for Debug build"
    classDirectories = fileTree(
            dir: "$buildDir/intermediates/classes/debug",
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/BuildConfig.*',
                       '**/*$ViewInjector*.*',
                       '**/*$ViewBinder*.*',
                       '**/Manifest*.*',
                       'android/**/*.*',
                       '**/Lambda$*.class', //Retrolambda
                       '**/Lambda.class',
                       '**/*Lambda.class',
                       '**/*Lambda*.class',
                       '**/*Lambda*.*',
                       '**/*$Lambda$*.*',
                       '**/*$lambda$*.*',
                       '**/*Builder.*',
                       '**/*_MembersInjector.class',  //Dagger2 generated code
                       '**/*_MembersInjector*.*',  //Dagger2 generated code
                       '**/*_*Factory*.*', //Dagger2 generated code
                       '**/*Component*.*', //Dagger2 generated code
                       '**/*Module*.*' //Dagger2 generated code
    ])
    if (project.hasProperty("teamcity")) {
        println '##teamcity[jacocoReport dataPath=\'app/build/jacoco/testDebugUnitTest.exec\' includes=\'com.vgaidarji.cimatters.*\' excludes=\'com.vgaidarji.cimatters.test.* **/*R*.* **/*Injector*.* **/*Activity*.* .*R .*CiMattersApplication .*BuildConfig .*Activity .*Test \']'
    }
    reports {
        xml.enabled = true
        html.enabled = true
    }
    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    executionData = files("$buildDir/jacoco/testDebugUnitTest.exec")
    // Bit hacky but fixes https://code.google.com/p/android/issues/detail?id=69174. 
    // We iterate through the compiled .class tree and rename $$ to $. 
    doFirst {
        new File("$buildDir/intermediates/classes/").eachFileRecurse { file ->
            if (file.name.contains('$$')) {
                file.renameTo(file.path.replace('$$', '$'))
            }
        }
    }
}

tasks.withType(Test) {
    testLogging {
        events "passed", "skipped", "failed"
    }
}